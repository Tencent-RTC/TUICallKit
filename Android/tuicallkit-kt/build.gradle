import org.gradle.util.VersionNumber

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 34
    namespace "com.tencent.qcloud.tuikit.tuicallkit"

    defaultConfig {
        versionName "3.5.0.0"
        versionCode 1
        minSdkVersion 19
        targetSdkVersion 34
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def projectJavaVersion = JavaVersion.VERSION_1_8
    def projectKotlinVersion = "1.8"
    VersionNumber currentGradleVersion = VersionNumber.parse(gradle.gradleVersion)
    if (currentGradleVersion.major >= 8) {
        projectJavaVersion = JavaVersion.VERSION_17
        projectKotlinVersion = "17"
    }

    compileOptions {
        sourceCompatibility projectJavaVersion
        targetCompatibility projectJavaVersion
    }
    kotlinOptions {
        jvmTarget = projectKotlinVersion
    }
}

dependencies {
    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.4.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api "com.google.code.gson:gson:2.9.1"

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    if (projects.contains("tuicore")) {
        api project(':tuicore')
    } else {
        api "com.tencent.imsdk:imsdk-plus:8.7.7207"
        api "com.tencent.liteav.tuikit:tuicore:8.7.7202"
    }

    if (projects.contains("engine_source")) {
        api project(':engine_source')
    } else {
        api rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_Professional:12.8+"
        api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "io.trtc.uikit:rtc_room_engine:3.5.+"
    }
    if (projects.contains("common")) {
        api project(':common')
    } else {
        api 'io.trtc.uikit:common:3.5.+'
    }

    //For FCM data message
    implementation "androidx.work:work-runtime-ktx:2.7.1"
}
